# -*- mode: sh -*- my zshell prompt configuration.

# async prompt template curtesy of [[https://gist.github.com/denysdovhan/e83dec6f09b237acbc24a6bb25fabd13][Denys Dovhan]].
typeset -Ag prompt_data # cache variable

DEFAULT_PROMPT='%F{white}Î»%f%b '
DEFAULT_PROMPT_RHS='%B%F{green}%n@%m%f %F{yellow}%~%f'

function generate_icon_times() { #(times max icon)
    if [ "$1" -le "$2" ]; then
        printf "$3"'%0.s ' $(seq 1 "$1")
    else
        prinft "%d %s" "$1" "$3"
    fi
}

# include an indicator for the current depth of lf (my file manager) levels
if [ -n "$LF_LEVEL" ]; then
    DEFAULT_PROMPT_RHS="$(print -n '%F{yellow}%B%s%b' "$(generate_icon_times "$LF_LEVEL" 5 'ï“ ')")$DEFAULT_PROMPT_RHS"
fi

unset -f generate_icon_times

if ! hash __git_prompt 1>/dev/null 2>&1; then
  git_prompt_path="$HOME/.local/bin/git-prompt"
  if [ -f "$git_prompt_path" ]; then
    ENTRY_POINT='true' . "$git_prompt_path"
  else
    echo "bash_ps1(error) : git prompt script not found: $git_prompt_path" >&2
    __git_prompt() { echo ""; }
  fi
  unset git_prompt_path
fi

# â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
# â”‚                      Utils                     â”‚
# â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/lib/utils.zsh#L10][spaceship/utils]].
function prompt::exists() {
  command -v $1 > /dev/null 2>&1
}

function prompt::project_root() {
  "$1" && cd -q "$1"
  git rev-parse --show-toplevel 2>/dev/null
}

# â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
# â”‚                Prompt Sections                 â”‚
# â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜

prompt_git_branch() {
  print -n '%F{blue}%B('"$(__git_prompt)"')%b'
}

prompt_git_status() {
  local status_flags='' status_color='magenta'
  INDEX=$(command git status --porcelain -b 2>/dev/null)

  # Check for untracked files
  if grep -E '^\?\? ' <<< "$INDEX" &>/dev/null; then
    status_flags="?$status_flags"
  fi

  # Check for staged files
  if grep -e '^A[ MDAU] ' -e '^M[ MD] ' -e '^UA' <<< "$INDEX" &>/dev/null; then
    status_flags="+$status_flags"
  fi

  # Check for modified files
  if grep '^[ MARC]M ' <<< "$INDEX" &>/dev/null; then
    status_flags="!$status_flags"
  fi

  # Check for renamed files
  if grep '^R[ MD] ' <<< "$INDEX" &>/dev/null; then
    status_flags="Â»$status_flags"
  fi

  # Check for deleted files
  if grep -e '^[MARCDU ]D ' -e '^D[ UM] ' <<< "$INDEX" &>/dev/null; then
    status_flags="âœ˜$git_status"
  fi

  # Check for stashes
  if git rev-parse --verify refs/stash >/dev/null 2>&1; then
    status_flags="S$status_flags"
  fi

  # Check for unmerged files
  if grep -e '^U[UDA] ' -e '^AA ' -e '^DD ' -e '^[DA]U ' <<< "$INDEX" &>/dev/null; then
    git_status="=$git_status"
  fi

  # Check whether branch is ahead
  local is_ahead=false
  if grep '^## [^ ]\+ .*ahead' <<< "$INDEX" &>/dev/null; then
    is_ahead=true
  fi

  # Check whether branch is behind
  local is_behind=false
  if grep '^## [^ ]\+ .*behind' <<< "$INDEX" &>/dev/null; then
    is_behind=true
  fi

  # Check wheather branch has diverged
  if [ "$is_ahead" = true -a "$is_behind" = true ]; then
    status_flags="â‡•$status_flags"
  else
    [ "$is_ahead"  = true ] && status_flags="â‡¡$status_flags"
    [ "$is_behind" = true ] && status_flags="â‡£$status_flags"
  fi

  [ -n "$status_flags" ] && print -n '%F{'"$status_color"'} ['"$status_flags"']'
}

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/sections/git_status.zsh][spaceship-prompt/git-status]].
prompt_git() {
  cd -q "$1"
  git rev-parse --is-inside-work-tree >/dev/null 2>&1 || return

  prompt_git_branch
  # prompt_git_status
}

prompt_root() {
  if [ "$EUID" -eq 0 ]; then
    print -n '%F{yellow}%B'"ğŸ‘‘"'%b'
  fi
}

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/sections/node.zsh][spaceship-prompt/node]].
prompt_node() {
  cd -q "$1"
  # only show the prompt section in node projects
  [ -f "package.json" -o -d node_modules ] || return

  local node_version
  if prompt::exists nvm; then
    node_version=$(nvm current 2>/dev/null)
    [[ $node_version == "system" || $node_version == "node" ]] && return
  elif prompt::exists nodenv; then
    node_version=$(nodenv version-name)
    [[ $node_version == "system" || $node_version == "node" ]] && return
  elif prompt::exists node; then
    node_version=$(node -v 2>/dev/null)
  else
    return
  fi

  print -n '%F{green}%B'"â¬¢ %b${node_version}"
}

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/sections/golang.zsh#L34][spaceship-prompt/golang]].
prompt_golang() {
  cd -q "$1"
  [[ -f go.mod || -d Godeps || -f glide.yaml || -f Gopkg.toml || -f Gopkg.lock \
       || ( $GOPATH && "$PWD/" =~ "$GOPATH/" ) ]] || return

  prompt::exists go || return
  local go_version=$(go version | awk '{
  if ($3 ~ /^devel/) {
    print $3 ":" substr($4, 2)
  } else {
    print "v" substr($3, 3)
  }
}
')

  print -n '%F{cyan}%B'"ğŸ”¹%b${go_version}"
}

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/sections/pyenv.zsh][spaceship-prompt/pyenv]].
prompt_python() {
  cd -q "$1"

  local py_exec='python'
  if [ -n "$PIPENV_ACTIVE" -o -f "Pipfile" -o "Pipfile.lock" ] && prompt::exists pipenv; then
    py_exec=$(pipenv --py)
  elif [ -f "requirements.txt" ] && prompt::exists "$py_exec"; then
    :
  else
    return
  fi
  local python_version=$($py_exec --version)
  python_version="${python_version#* }"
  [ -z "$python_version" ] && return

  print -n '%F{yellow}'"ğŸ (v${python_version})"
}

# see [[https://github.com/denysdovhan/spaceship-prompt/blob/master/sections/ruby.zsh][spaceship-prompt/ruby]].
prompt_ruby() {
  cd -q "$1"
  [ -f Gemfile -o -f Rakefile ] || return

  local ruby_version
  if prompt::exists rvm-prompt; then
    ruby_version=$(rvm-prompt i v g)
  elif prompt::exists chruby; then
    ruby_version=$(chruby | sed -n -e 's/ \* //p')
  elif prompt::exists rbenv; then
    ruby_version=$(rbenv version-name)
  elif prompt::exists asdf; then
    # split output on space and return first element
    ruby_version=${$(asdf current ruby)[1]}
  elif prompt::exists ruby; then
    ruby_version=$(ruby -e 'puts "v#{RUBY_VERSION}"')
  else
    return
  fi

  [ -z "$ruby_version" -o "${ruby_version}" = "system" ] && return

  # ensure ruby version begins with a "v".
  [[ "${ruby_version}" =~ ^[0-9].+$ ]] && ruby_version="v${ruby_version}"

  print -n '%F{red}%B'"ğŸ’ %b${ruby_version}"
}

# â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
# â”‚                   Construct                    â”‚
# â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜

prompt_join() {
  local res=''
  for arg in "$@"; do
    if [ -n "$arg" ]; then
      if [ -z "$res" ]; then
        res="$arg"
      else
        res="$res $arg"
      fi
    fi
  done

  if [ -n "$res" ]; then
    echo "$res "
  fi
}

prompt_refresh() {
  local data_rendered=$(prompt_join                      \
                          "$prompt_data[prompt_root]"    \
                          "$prompt_data[prompt_git]"     \
                          "$prompt_data[prompt_node]"    \
                          "$prompt_data[prompt_golang]"  \
                          "$prompt_data[prompt_ruby]"    \
                          "$prompt_data[prompt_python]"  \
        )
  PS1="${data_rendered}$DEFAULT_PROMPT"
  RPS1="$DEFAULT_PROMPT_RHS"

  zle && zle .reset-prompt # Trigger prompt redraw after update
}

# â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
# â”‚                     Setup                      â”‚
# â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜

prompt_callback() {
  local job=$1 code=$2 output=$3 exec_time=$4

  # See [[https://github.com/mafredri/zsh-async/issues/42#issuecomment-716782220][zsh-async]], this evidently fixes issues with crashing workers
  if [ "$code" -eq 2 -o "$code" -eq 3 -o "$code" -eq 130 ]; then
    # reinit async workers
    async_stop_worker 'prompt' # stop the current worker
    prompt_init_worker         # start the workers again
  fi

  prompt_data[$job]=$output
  prompt_refresh
}

prompt_init_worker() {
  async_start_worker 'prompt' -n
  async_register_callback 'prompt' prompt_callback
}
prompt_init_worker

# start async jobs before cmd
prompt_precmd() {
  root=$(prompt::project_root "$PWD")
  async_job 'prompt' prompt_root
  async_job 'prompt' prompt_git "$PWD"
  async_job 'prompt' prompt_node "${root:-$PWD}"
  async_job 'prompt' prompt_golang "${root:-$PWD}"
  async_job 'prompt' prompt_ruby "${root:-$PWD}"
  # async_job 'prompt' prompt_python "${root:-$PWD}"
}

add-zsh-hook precmd prompt_precmd

PS1=$DEFAULT_PROMPT
RPS1=$DEFAULT_PROMPT_RHS
